#!/usr/bin/env perl

# Print my CPAN test failures in the last 3 days

use warnings;
use strict;
use autodie qw(:all);
use Data::Dumper;

no lib '.';

use constant ONE_HOUR => 60 * 60;
use constant ONE_DAY => 24 * ONE_HOUR;
use constant THREE_DAYS => ONE_DAY * 3;

my @needfull;

# Install Pre-requisites
BEGIN {
	my @modules = (
		'DateTime', 'Date::Parse', 'File::HomeDir',
		'File::Spec', 'HTTP::Cache::Transparent',
		'LWP::Simple::WithCache', 'XML::Hash',
	);

	foreach my $module(@modules) {
		eval "use $module";
		if($@) {
			push @needfull, $module;
		}
	}
	if(scalar(@needfull)) {
		my $list = join(' ', @needfull);
		print "Installing $list\n";
		system("cpan -i $list");
	}
}

foreach my $module(@needfull) {
	my $version;
	if($module =~ /(.+)\s(.+)$/) {
		$module = $1;
		$version = $2;
	}
	if($module =~ /.+\/(.+)/) {
		$module = $1;
	}
	eval "require $module";
	if($@) {
		die $@;
	}
	$module->import();
	# if($version && ($module::VERSION < $version)) {
		# die "$module: need $version got ", $module::VERSIOn;
	# }
}

my $cache_dir = $ENV{'CACHE_DIR'};
if($cache_dir) {
	mkdir $cache_dir, 0700 if(!-d $cache_dir);
	$cache_dir = File::Spec->catfile($cache_dir, 'http-cache-transparent');
} else {
	# $cache_dir = File::Spec->catfile(File::Spec->tmpdir(), 'cache', 'http-cache-transparent');
	$cache_dir = File::Spec->catfile(File::HomeDir->my_home(), '.cache', 'http-cache-transparent');
}

HTTP::Cache::Transparent::init({
	BasePath => $cache_dir,
	# Verbose => $opts{'v'} ? 1 : 0,
	NoUpdate => ONE_HOUR,
	MaxAge => 30 * 24
}) || die "$0: $cache_dir: $!";

my $content = LWP::Simple::WithCache::get('http://cpantesters.org/author/N/NHORNE-nopass.rss');
die if(!defined($content));

my $now = DateTime->now();

# my $x = XML::Hash->new()->fromXMLStringtoHash($content)->{'rss'}->{'channel'}->{'item'};
# print Data::Dumper->new([$x])->Dump();

for my $item(@{XML::Hash->new()->fromXMLStringtoHash($content)->{'rss'}->{'channel'}->{'item'}}) {
	my $pubdate = str2time($item->{'pubDate'}->{'text'});

	my $ago = $now->epoch() - $pubdate;
	if($ago <= THREE_DAYS) {
		print "$ago seconds ago:\n\t", $item->{'title'}->{'text'}, ': ', $item->{'link'}->{'text'}, "\n";
	}
}
